start: instruction*

instruction: declaration
    | assignation
    | afficher
    | ajout ";"
    | condition
    | boucle
    | exp ";"

declaration.-5: TYPE VARIABLE ("=" exp)? ";"

TYPE.20: "booléen" | "entier" | "texte" | "liste"
VARIABLE: ("a".."z" | "A".."Z" | "é" | "É" | "è" | "È" | "à" | "À" | "ù" | "Ù" | "â" | "Â" | "ê" | "Ê" | "û" | "Û" | "î" | "Î" | "ô" | "Ô" | "ŷ" | "Ŷ" | "_" ) ("a".."z" | "A".."Z" | "é" | "É" | "è" | "È" | "à" | "À" | "ù" | "Ù" | "â" | "Â" | "ê" | "Ê" | "û" | "Û" | "î" | "Î" | "ô" | "Ô" | "ŷ" | "Ŷ" | "_" | "0".."9")*

assignation.-5: VARIABLE "=" exp ";"

afficher.-5: "afficher" ((exp | ajout) ",")* (exp | ajout) ";"

ajout.-5: "ajouter" exp "dans" VARIABLE #| exp

condition: si | sisinon
si.-5: "si" exp "alors" "{" instruction* "}"
#sisinon.-4: "si" exp "alors" "{" instruction* "}" "sinon" "{" instruction* "}"
sisinon.-4: si "sinon" "{" instruction* "}"

boucle: tantque | pourchaque
tantque.-5: "tant" "que" exp "faire" "{" instruction* "}"
pourchaque.-5: "pour" "chaque" TYPE VARIABLE "dans" exp "faire" "{" instruction* "}"

exp: literal | operation | parenthese | VARIABLE
parenthese.15: "(" exp ")"

literal: BOOLEEN | ENTIER | TEXTE | leslistes
BOOLEEN.20: "vrai" | "faux"
ENTIER.20: ("+")? (("1".."9" ("0".."9")*) | "0")
TEXTE.20: /\"[^\"]*\"/
leslistes: liste | sequence
liste.20: "[" "]" | "[" (exp ",")* exp "]"
sequence.20: "[" exp ":" exp "]"

operation: egalite
    | nonegalite
    | pluspetit
    | plusgrand
    | pluspetitouegal
    | plusgrandouegal
    | et
    | ou
    | non
    | negation
    | addition
    | soustraction
    | multiplication
    | division
    | indice
    | taille

egalite.5: (exp "vaut" exp) | (exp "==" exp)
nonegalite.5: (exp "ne" "vaut" "pas" exp) | (exp "!=" exp)
pluspetit.5: exp "<" exp
plusgrand.5: exp ">" exp
pluspetitouegal.5: exp "<=" exp
plusgrandouegal.5: exp ">=" exp
et.5: exp "et" exp
ou.5: exp "ou" exp
non.10: "non" exp

negation.10: "-" exp
addition.7: exp ("+" exp)+
soustraction.7: exp ("-" exp)+
multiplication.7: exp ("*" exp)+
division.7: exp ("/" exp)+

indice.10: exp "[" exp "]"
taille.10: "taille" (leslistes | VARIABLE | literal)

NOESPACE: /[ \t\f\r\n]/+
%ignore NOESPACE
COMMENT: "#" /[^\n]/*
%ignore COMMENT
